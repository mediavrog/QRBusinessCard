apply plugin: 'android'

android {
    compileSdkVersion 19
    buildToolsVersion "19.1.0"

    defaultConfig {
        minSdkVersion 8
        targetSdkVersion 19
        versionCode 2014052901
        versionName "0.0.1"
    }

    String securePropertiesPath = project.projectDir.path + "/secure.properties"
    if (new File(securePropertiesPath).exists()) {
        Properties props = new Properties()
        props.load(new FileInputStream(file(securePropertiesPath)))

        signingConfigs {
            release {
                storeFile file(props['key.store'])
                storePassword props['key.store.password']
                keyAlias props['key.alias']
                keyPassword props['key.alias.password']
            }
        }
    } else {
        System.out.println("WARNING: Could not find secure.properties")
        // Building from IDE's "Run" button
        signingConfigs {
            release {}
        }
    }

    buildTypes {
        debug {
            packageNameSuffix ".debug"
            versionNameSuffix "|D"
            buildConfigField "boolean", "DEBUG_FLAG", "true"
            buildConfigField "boolean", "LOG", "true"
            buildConfigField "boolean", "IS_BETA", "true"
        }
        release {
            signingConfig signingConfigs.release
            runProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            buildConfigField "boolean", "DEBUG_FLAG", "false"
            buildConfigField "boolean", "LOG", "false"
            buildConfigField "boolean", "IS_BETA", "true"
        }
    }
}

dependencies {
    compile 'com.googlecode.ez-vcard:ez-vcard:0.9.3'
    compile 'com.google.zxing:core:2.2'
    compile 'com.google.zxing:javase:2.2'
    compile 'com.squareup.picasso:picasso:2.3.1'
    compile 'com.android.support:appcompat-v7:19.1.0'
    compile 'com.android.support:support-v4:19.1.0'
    compile fileTree(dir: 'libs', include: ['*.jar'])
}
